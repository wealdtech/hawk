subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'sonar'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {
        testCompile 'org.testng:testng:6.8'
    }

    group = 'com.wealdtech.hawk'
    version = '0.9.0'

    task copyLibs (type: Copy) {
        into "$buildDir/output/libs"
        from configurations.testRuntime
    }

    task testJar (type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

//                repository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
//                    authentication(userName: sonatypeUsername, password: sonatypePassword)
//                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    packaging 'jar'
                    url 'https://github.com/wealdtech/hawk'

                    scm {
                        url 'scm:git@github.com:wealdtech/hawk.git'
                        connection 'scm:git@github.com:wealdtech/hawk.git'
                        developerConnection 'scm:git@github.com:wealdtech/hawk.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'wealdtech'
                            name 'Weald Technology'
                        }
                    }
                }
            }
        }
    }

  sonar
  {
    server
    {
      url="http://10.0.0.101:9000/"
    }
    database
    {
      url = "jdbc:h2:tcp://10.0.0.101/sonar"
    }
    project
    {
      language="java"
      sourceEncoding = "UTF-8"
    }
  }
}
